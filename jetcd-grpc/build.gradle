/*
 * Copyright 2016-2021 The jetcd authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    alias(libs.plugins.protobuf)
}

dependencies {
    api libs.slf4j
    api libs.javaxAnnotation
    api libs.vertxGrpc
    api libs.guava
    api libs.bundles.grpc
}

protobuf {
    protoc {
        if (osdetector.os == "osx") {
            artifact = "com.google.protobuf:protoc:${libs.versions.protoc.get()}:osx-x86_64"
        } else {
            artifact = "com.google.protobuf:protoc:${libs.versions.protoc.get()}"
        }
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${libs.versions.grpc.get()}"
        }
        vertx {
            artifact = "io.vertx:vertx-grpc-protoc-plugin:${libs.versions.vertx.get()}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc
            vertx
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs "${projectDir}/build/generated/source/proto/main/java"
            srcDirs "${projectDir}/build/generated/source/proto/main/grpc"
            srcDirs "${projectDir}/build/generated/source/proto/main/vertx"
        }
    }
}